apply plugin: 'groovy'

String gradleVersion = '2.13'

ext.scriptFile = { String fileName ->
    return new File(new File(rootDir, 'gradle'), fileName)
}

apply from: scriptFile('compiler-settings.gradle')

repositories {
    mavenCentral()

    maven {
        url 'http://repo.gradle.org/gradle/libs-releases-local'
    }

    maven {
        url 'http://repo.gradle.org/gradle/libs-snapshots-local'
    }

    maven { url 'http://dl.bintray.com/kelemen/maven' }

    ivy {
        // This "repository" was added only as a hack to download
        // the Gradle distribution at http://services.gradle.org/distributions/gradle-1.8-bin.zip
        url 'http://services.gradle.org'
        layout 'pattern', {
            artifact '[organisation]/[artifact]-[revision].[ext]'
        }
    }
}

sourceSets {
    main {
        groovy.srcDirs = []
    }
    modelBuilders {
        compileClasspath += main.output
        java.srcDirs = []
    }
    test {
        groovy.srcDirs = []

        compileClasspath += configurations.modelBuildersCompile
        runtimeClasspath -= main.output
        runtimeClasspath += files(jar.archivePath)
    }
}

configurations {
    modelBuildersCompile.extendsFrom compile
    modelBuildersCompile.exclude module: 'gradle-tooling-api'
}

configureJavaCompilers(7)

compileTestJava.dependsOn(jar)

jar {
    from sourceSets.modelBuilders.output
    ext {
        netBeansSourceSets = [sourceSets.main, sourceSets.modelBuilders]
    }
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
    from sourceSets.modelBuilders.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

dependencies {
    compileOnly "com.github.kelemen:netbeans-gradle-plugin:${nbGradleVersion}"
    modelBuildersCompileOnly "com.github.kelemen:netbeans-gradle-plugin:${nbGradleVersion}"

    modelBuildersCompileOnly group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.8.6'

    def gradleApiDependency = new org.netbeans.gradle.build.ZippedJarsFileCollection(
        project,
        'distributions:gradle:1.8-bin@zip',
        ['gradle-api'],
        ['gradle-1.8', 'lib']
    )
    modelBuildersCompileOnly gradleApiDependency

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

test.systemProperty 'TESTED_GRADLE_DAEMON_VERSION', gradleVersion

afterEvaluate {
    System.setProperty('line.separator', '\n')
}
